*** Settings ***

Resource  plone/act/keywords.txt

*** Test cases ***

Simple Search
    Enter searchterm  Welcome
    Livesearch results contain  Welcome to Plone
    Click search button
    Search results contain  Welcome to Plone

Livesearch for pages
    Count livesearch items for  livesearch test page  4

Livesearch for folder
    Count livesearch items for  livesearch test folder  2

Livesearch within folder
    Click Link  link=A livesearch test folder
    Wait Until Page Contains Element  css=input#searchbox_currentfolder_only
    Select Checkbox  css=input#searchbox_currentfolder_only
    Count livesearch items for  livesearch test page  3

*** Keywords ***

Start browser
    Open browser  http://localhost:55001/plone/

Enter searchterm
    [Arguments]  ${searchtext}
    Input text  css=input#searchGadget  ${searchtext}
    Focus  css=input#searchGadget

Click search button
    Click Button  css=.LSBox .searchButton

Livesearch results contain
    [Arguments]  ${text}
    Wait until keyword succeeds  5s  1s  Element should contain  css=#LSResult .LSRow a  ${text}

Search results contain
    [Arguments]  ${text}
    Wait until keyword succeeds  5s  1s  Element should contain  css=.searchResults dt a  ${text}

Count livesearch items For
    [Arguments]  ${string}  ${count}
    Enter searchterm  ${string}
    Wait until keyword succeeds  5s  1s  Element Should Be Visible  css=div#LSResult
    Wait until keyword succeeds  5s  1s  Xpath Should Match X Times  //div[@id = 'LSResult']/descendant::li  ${count}
    Focus  css=input#searchbox_currentfolder_only
    Wait until keyword succeeds  5s  1s  Element Should Not Be Visible  css=div#LSResult
