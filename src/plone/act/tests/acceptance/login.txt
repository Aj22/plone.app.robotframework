*** Settings ***

Library  Selenium2Library  run_on_failure=Capture Page Screenshot
Library  plone.act.PloneLibrary
Variables  plone/app/testing/interfaces.py
Resource  plone/act/keywords.txt

Suite Setup  Start browser
Suite Teardown  Close All Browsers

*** Variables ***

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone


*** Test cases ***

Test Valid Login
    Given the overlay login form
    When I enter valid credentials
    Then I am logged in

Test Invalid Login
    Given the overlay login form
    When I enter invalid credentials
    Then I am not logged in


*** Keywords ***

Start browser
    Open browser  ${PLONE_URL}

the overlay login form
    Go to  ${PLONE_URL}
    Click Overlay Link  Log in

I enter valid credentials
    Input text  __ac_name  ${TEST_USER_NAME}
    Input text  __ac_password  ${TEST_USER_PASSWORD}
    Click Button  Log in

I enter invalid credentials
    Input text  __ac_name  ${TEST_USER_NAME}
    Input text  __ac_password  ""
    Click Button  Log in

I am logged in
    Wait until page contains  ${TEST_USER_ID}

I am not logged in
    Wait until page contains  Login failed
