*** Settings ***

Library  Selenium2Library  run_on_failure=Capture Page Screenshot
Variables  plone/app/testing/interfaces.py
Resource  plone/act/plone.txt

Suite Setup  Start browser and create some content
Suite Teardown  Clean up

*** Variables ***

${PORT} =  55001
${ZOPE_URL} =  http://localhost:${PORT}
${PLONE_URL} =  ${ZOPE_URL}/plone


*** Test cases ***

Simple Search
    Enter searchterm  Welcome
    Livesearch results contain  Welcome to Plone
    Click search button
    Search results contain  Welcome to Plone

Livesearch for pages
    Count livesearch items for  livesearch test page  4

Livesearch for folder
    Count livesearch items for  livesearch test folder  2

Livesearch within folder
    Click Link  link=A livesearch test folder
    Select Checkbox  css=input#searchbox_currentfolder_only
    Count livesearch items for  livesearch test page  3

*** Keywords ***

Start browser
    Open browser  http://localhost:55001/plone/

Create test content
    Log in as site owner
    Goto homepage
    Add Page  Welcome to Plone
    Goto homepage
    Add Page  A livesearch test page
    Goto homepage
    Create folder  A livesearch test folder
    Add Page  A livesearch test page 1
    Go to  ${PLONE_URL}/a-livesearch-test-folder
    Add Page  A livesearch test page 2
    Log out
    Goto homepage

Start browser and create some content
    Start browser
    Create test content

Enter searchterm
    [Arguments]  ${searchtext}
    Input text  css=input#searchGadget  ${searchtext}
    Focus  css=input#searchGadget

Click search button
    Click Button  css=.LSBox .searchButton

Livesearch results contain
    [Arguments]  ${text}
    Wait until keyword succeeds  5s  1s  Element should contain  css=#LSResult .LSRow a  ${text}

Search results contain
    [Arguments]  ${text}
    Wait until keyword succeeds  5s  1s  Element should contain  css=.searchResults dt a  ${text}

Count livesearch items For
    [Arguments]  ${string}  ${count}
    Search for  ${string}
    Wait until keyword succeeds  5s  1s  Element Should Be Visible  css=div#LSResult
    Wait until keyword succeeds  5s  1s  Xpath Should Match X Times  //div[@id = 'LSResult']/descendant::li  ${count}
    Focus  css=input#searchbox_currentfolder_only
    Wait until keyword succeeds  5s  1s  Element Should Not Be Visible  css=div#LSResult

Clean up
#    Remove test content
    Close all browsers

Remove test content
    Log in as site owner
    Remove Content  welcome-to-plone
    Remove Content  a-livesearch-test-page
    Remove Content  a-livesearch-test-page-1
    Remove Content  a-livesearch-test-page-2
    Log out
    Goto homepage
