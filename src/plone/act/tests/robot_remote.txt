*** Settings ***

Resource  plone/act/keywords.txt
Library  Remote  ${PLONE_URL}/RemoteKeywordsLibrary

Test Setup  Open Plone Root
Test Teardown  Close All Browsers

*** Test cases ***

Some default product is activated
    Given 'plone.app.jquery' is activated
      and 'plone.app.discussion' is not activated

Site Administrator can access control panel
    Given I'm logged in as a 'Site Administrator'
     When I open the personal menu
     Then I see the Site Setup -link

Contributor cannot access control panel
    Given I'm logged in as a 'Contributor'
     When I open the personal menu
     Then I cannot see the Site Setup -link

Contributor can create a folder
    Given I'm logged in as a 'Contributor'
     When I go to the front page
     Then I can add a folder

Owner can add a sub-folder under her own folder
    Given I've created a test folder
      And I'm logged in as 'Authenticated'
     When I go to my test folder
     Then I can add a sub-folder

Authenticated cannot add a sub-folder under other's folder
    Given Someone has created a test folder
      And I'm logged in as 'Authenticated'
     When I go to the test folder
     Then I get Insufficient Privileges error


*** Keywords ***

I go to the front page
    Go to  ${PLONE_URL}

I can add a folder
    Add folder  Test folder

I've created a test folder
    Enable autologin as  Contributor
    Set autologin username  Authenticated
    Go to  ${PLONE_URL}
    Add folder  Test folder
    Go to  ${PLONE_URL}/test-folder
    Page should contain  Test folder
    Element should contain  css=.documentAuthor  Authenticated

I'm logged in as 'Authenticated'
    Enable autologin as  Authenticated

I go to my test folder
    Go to  ${PLONE_URL}/test-folder

I can add a sub-folder
    Add folder  Sub folder
    Go to  ${PLONE_URL}/test-folder/sub-folder
    Page should contain  Sub folder

Someone has created a test folder
    Enable autologin as  Contributor
    Go to  ${PLONE_URL}
    Add folder  Test folder

I go to the test folder
    Go to  ${PLONE_URL}/test-folder

I get Insufficient Privileges error
    Page should contain  Insufficient Privileges

'${product}' is activated
    Product is activated  ${product}

'${product}' is not activated
    Run keyword and expect error  *  Product is activated  ${product}

I'm logged in as a '${ROLE}'
    Enable autologin as  ${ROLE}
    Go to  ${PLONE_URL}

I open the personal menu
    Click link  css=#user-name

I see the Site Setup -link
    Element should be visible  css=#personaltools-plone_setup

I cannot see the Site Setup -link
    Element should not be visible  css=#personaltools-plone_setup

I pause for debugging
    Set Selenium Timeout  600 seconds
    Wait For Condition  true
