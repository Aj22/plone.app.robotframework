*** Settings ***

Resource  plone/act/keywords.txt
Library  Remote  ${PLONE_URL}/RemoteKeywordsLibrary

Test Setup  Open Plone Root
Test Teardown  Close All Browsers

*** Test cases ***

Some default product has been activated
    Given 'plone.app.jquery' has been activated
      and 'plone.app.discussion' has not been activated

Site Administrator can access control panel
    Given I'm logged in as a 'Site Administrator'
     When I open the personal menu
     Then I see the Site Setup -link

Contributor cannot access control panel
    Given I'm logged in as a 'Contributor'
     When I open the personal menu
     Then I cannot see the Site Setup -link

*** Keywords ***

'${product}' has been activated
    Product has been activated  ${product}

'${product}' has not been activated
    Run keyword and expect error  *  Product has been activated  ${product}

I'm logged in as a '${ROLE}'
    Autologin as  ${ROLE}
    Sleep  1 s
    Go to  ${PLONE_URL}

I open the personal menu
    Click link  css=#user-name

I see the Site Setup -link
    Element should be visible  css=#personaltools-plone_setup

I cannot see the Site Setup -link
    Element should not be visible  css=#personaltools-plone_setup
